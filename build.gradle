buildscript {
    repositories { mavenCentral() }
    dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.3-2" }
}

plugins {
    id "org.springframework.boot" version "1.5.6.RELEASE"
    id "org.jetbrains.kotlin.plugin.spring" version "1.1.3-2"
}

ext {
    springVersion = "4.3.10.RELEASE"
    springBootVersion = "1.5.6.RELEASE"
    camelVersion = "2.18.1"
    activeMQClientVersion = "5.14.1"
    kotlinVersion = "1.1.3-2"
}

apply plugin: 'application'
apply plugin: 'kotlin'

version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

subprojects {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
        compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
        compile "org.projectlombok:lombok:1.16.0"
        compile 'com.google.guava:guava-parent:20.0'
        testCompile 'org.hamcrest:hamcrest-core:1.3'
        testCompile 'org.mockito:mockito-core:2.8.9'
        testCompile 'junit:junit:4.12'
    }
}

project(":spring-amqp-rabbitmq") {
    dependencies {
        compile project(':commons')
        compile "org.springframework.boot:spring-boot-starter-amqp:${springBootVersion}"
        compile "org.springframework:spring-messaging:${springVersion}"
        compile "org.springframework.integration:spring-integration-http:${springVersion}"
    }
}

project(":spring-jms-simple") {
    dependencies {
        compile project(":commons")
        compile "org.springframework:spring-jms:${springVersion}"
        compile "org.apache.activemq:activemq-client:${activeMQClientVersion}"
        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }
}

project(":file-transfer-pattern") {
    dependencies {
        compile "org.apache.camel:camel-core:$camelVersion"
        compile "org.apache.camel:camel-ftp:$camelVersion"
    }
}

configure(subprojects.findAll { it.path.startsWith(':spring-integration') }) {
    dependencies {
        compile project(":commons")
        compile "org.springframework.integration:spring-integration-core:${springVersion}"
        compile "org.springframework.integration:spring-integration-file:${springVersion}"
        compile "org.springframework.integration:spring-integration-ftp:${springVersion}"
        compile "org.springframework.integration:spring-integration-jdbc:${springVersion}"
        compile "org.springframework.integration:spring-integration-jms:${springVersion}"
        compile "org.springframework.integration:spring-integration-http:${springVersion}"
        compile "org.springframework.integration:spring-integration-xml:${springVersion}"
        compile "org.springframework.boot:spring-boot-starter-jdbc:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
        compile "org.apache.activemq:activemq-client:${activeMQClientVersion}"
        /*compile "com.fasterxml.jackson.core:jackson-core:2.8.9"
        compile "com.fasterxml.jackson.core:jackson-databind:2.8.9"
        compile "com.fasterxml.jackson.core:jackson-annotations:2.8.9"*/
        compile "com.h2database:h2:1.4.196"
    }

    mainClassName = "com.ddubson.integration.Application"
}